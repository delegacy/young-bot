apply plugin: 'org.springframework.boot'
apply plugin: 'com.bmuschko.docker-remote-api'

dependencies {
    implementation project(':common')

    compile 'com.linecorp.armeria:armeria-spring-boot2-webflux-starter'

    // Spring Data JPA
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-validation'
    compile 'com.h2database:h2'
    runtime 'mysql:mysql-connector-java'

    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'io.micrometer:micrometer-registry-prometheus'

    compile 'com.linecorp.bot:line-bot-api-client'
    compile 'com.linecorp.bot:line-bot-parser'

    compile 'com.slack.api:bolt'
    compile 'javax.websocket:javax.websocket-api'
    compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client'

    compile 'com.google.guava:guava'
    compile 'org.apache.commons:commons-lang3'

    runtime 'ch.qos.logback:logback-classic'
    runtime 'org.slf4j:log4j-over-slf4j'
    runtime 'ch.qos.logback.contrib:logback-json-classic'
    runtime 'ch.qos.logback.contrib:logback-jackson'
}

jar {
    enabled = true
}

bootJar {
    classifier = 'boot'
}

springBoot {
    buildInfo()
}

docker {
    registryCredentials {
        username = project.hasProperty('dockerUsername') ? dockerUsername : System.getenv('DOCKER_USERNAME')
        password = project.hasProperty('dockerPassword') ? dockerPassword : System.getenv('DOCKER_PASSWORD')
    }
}

task dockerUnpackDependencies(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("$buildDir/docker/dependency")
}

task dockerCopyDockerfile(type: Copy) {
    from("$projectDir/Dockerfile")
    into("$buildDir/docker")
}

import com.bmuschko.gradle.docker.tasks.image.*
task dockerBuildImage(type: DockerBuildImage) {
    dependsOn dockerCopyDockerfile, dockerUnpackDependencies
    inputDir = file("$buildDir/docker")
    buildArgs.put('DEPENDENCY', 'dependency')
    images.add('delegacy/young-bot-server:latest')
}

task dockerPushImage(type: DockerPushImage) {
    dependsOn dockerBuildImage
    images.set(dockerBuildImage.images)
}
