buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

apply from: "${rootDir}/gradle/scripts/build-flags.gradle"

// Configure all Java projects.
configure(projectsWithFlags('java')) {
    // Common dependencies
    dependencies {
        if (project != project(':core')) {
            api project(':core')
        }

        // Guava
        implementation 'com.google.guava:guava'

        // Logging
        implementation 'org.slf4j:slf4j-api'

        // JSR305
        implementation 'com.google.code.findbugs:jsr305'

        testImplementation project(':testing-internal')

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-commons'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly 'org.junit.platform:junit-platform-engine'
        testRuntimeOnly 'io.projectreactor.tools:blockhound-junit-platform'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation 'org.awaitility:awaitility'
    }

    test {
        testLogging {
            // Set options for log level LIFECYCLE
            events 'failed'
            exceptionFormat 'short'

            // Set options for log level DEBUG
            debug {
                events 'started', 'skipped', 'failed'
                exceptionFormat 'full'
            }

            // Remove standard output/error logging from --info builds
            // by assigning only 'failed' and 'skipped' events
            info {
                events = ['failed', 'skipped']
            }
        }

        failFast = true
    }
}
