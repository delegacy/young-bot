buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
    }
}

plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE' apply false
    id 'org.sonarqube' version '3.0'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

apply from: "${rootDir}/gradle/scripts/build-flags.gradle"

subprojects {
    sonarqube {
        skipProject = true
    }
}

// Configure all Java projects.
configure(projectsWithFlags('java')) {
    // Common dependencies
    dependencies {
        testCompile 'org.springframework.boot:spring-boot-starter-test'
        // WebTestClient still requires this lib.
        testRuntimeOnly 'org.springframework.boot:spring-boot-starter-reactor-netty'

        testCompile 'org.junit.jupiter:junit-jupiter-api'
        testCompile 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-commons'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly 'org.junit.platform:junit-platform-engine'

        testCompile 'com.linecorp.armeria:armeria-junit5'
        testCompile 'io.projectreactor:reactor-test'
        testCompile 'org.mockito:mockito-junit-jupiter'
    }

    test {
        testLogging {
            // Set options for log level LIFECYCLE
            events 'failed'
            exceptionFormat 'short'

            // Set options for log level DEBUG
            debug {
                events 'started', 'skipped', 'failed'
                exceptionFormat 'full'
            }

            // Remove standard output/error logging from --info builds
            // by assigning only 'failed' and 'skipped' events
            info {
                events = ['failed', 'skipped']
            }
        }

        failFast = true
    }

    sonarqube {
        skipProject = false
    }
}
